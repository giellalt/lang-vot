!! =================================== !
!! The Votic morphophonological/twolc rules file !
!! =================================== !

Alphabet
a b č d e f g h i j k l m n o p r s š z ž t u v õ ä ö ü 
A B Č D E F G H I J K L M N O P R S Š Z Ž T U V Õ Ä Ö Ü 

d̕  !!= * @CODE@ d with U+0315
D̕  !!= * @CODE@ D with U+0315
t̕  !!= * @CODE t with U+0315
T̕  !!= * @CODE@ T with U+0315
l̕  !!= * @CODE@ l with U+0315
L̕  !!= * @CODE@ L with U+0315

ʼ !! U+02BC MODIFIER LETTER APOSTROPHE

!! Archiphones
 %{EAÕ%}:e	!!= @CODE@ Realized
 %{EAÕ%}:a	!!= @CODE@ Realized
 %{EAÕ%}:õ	!!= @CODE@ Realized

!! Triggers
 %^KS2S:0	!!= @CODE@ used in aluks:alus, perhaps also ps:s, ts:s
 %^WGStem:0	!!= @CODE@ used in mb:mm
 %^VowRm:0	!!= @CODE@ final vowel removal

 %> ;

Sets

 Vow = a e i o u õ ä ö ü 
       A E I O U Õ Ä Ö Ü ;
 Cns = b č d d̕ f g h j k l l̕ m n p r s š t t̕ v z ž 
       B Č D D̕ F G H J K L L̕ M N P R S Š T T̕ V Z Ž ;
 
Rules

!! VOWELS
"%{EAÕ%}:e"
!! **@RULENAME@**
 %{EAÕ%}:e <=> [ ä ä: | e e: | ü ü: | ä i ] %> h _ %{EAÕ%}: ;
               [ ä ä: | e e: | ü ü: | ä i ] %> h %{EAÕ%}: _ ;

"%{EAÕ%}:a"
!! **@RULENAME@**
 %{EAÕ%}:a <=> a a: %> h _ %{EAÕ%}: ;
               a a: %> h %{EAÕ%}: _ ;

"%{EAÕ%}:õ"
!! **@RULENAME@**
 %{EAÕ%}:õ <=> [ u u: | o o: | õ i ] %> h _ %{EAÕ%}: ;
               [ u u: | o o: | õ i ] %> h %{EAÕ%}: _ ;

"Deletion of Vx"
!! **@RULENAME@**
 Vx:0 <=> Vx _ %> h %{EAÕ%}: %{EAÕ%}: ;

       where Vx in ( a o u ä e ü ) ; 

"Deletion of õ "
!! **@RULENAME@**
 õ:0 <=>   _ ( %^WGStem:) %^VowRm: ;
"Deletion of a "
!! **@RULENAME@**
 a:0 <=>   _ ( %^WGStem:) %^VowRm: ;

!! ### CONSONANTS
!! WEAK GRADE
"Weakening of k to g"
!! **@RULENAME@**
 k:g <=> Vow: _ Vow:  %^WGStem: ;

!! REMOVAL
"Deleting k before stem-final s"
!! **@RULENAME@**
 k:0 <=> Vow _ s %^KS2S: ;

!!€# aluks%^KS2S
!!€0 alu0s0

"Deleting t in geminate tt"
!! **@RULENAME@**
 t:0 <=> Vow t _  Vow: %^WGStem: ;
!! tüttö+N+Sg+Gen: **girl/tyttö**
!!€ tüttö%^WGStem
!!€ tüt0ö0

"p:m in weak grade"
!! **@RULENAME@**
 p:m <=> m _ Vow: %^WGStem: ;

!!€# ampa%^WGStem%^VowRmõz
!!€0 amm000õz

